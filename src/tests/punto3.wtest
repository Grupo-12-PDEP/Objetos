import rolando.*

describe "Tests del punto 3: Lucha y hechicería avanzada" {

	const espadaDelDestino
	const collarDivino
	const mascaraOscura
	const armadura
	const espejo

	fixture {
		espadaDelDestino = new EspadaDelDestino()
		collarDivino = new CollarDivino()
		mascaraOscura = new MascaraOscura()
		armadura = new Armadura()
		espejo = new Espejo()
		collarDivino.perlas(5)
		rolando.artefactos([espadaDelDestino, collarDivino, mascaraOscura, armadura, espejo])
		rolando.hechizoPreferido(new EspectroMalefico())
	}

	test "Si rolando esta cargado, tiene 5 artefactos o más" {
		assert.that(rolando.estasCargado())
	}

	test "Si le sacamos la armadura, no está cargado" {
		rolando.removeArtefacto(armadura)
		assert.notThat(rolando.estasCargado())		
	}

	test "Si rolando tiene los artefactos de arriba, su habilidad de lucha es de 20" {
		assert.equals(rolando.habilidadDeLucha(), 20)
	}

	test "Si la armadura tiene la bendición como refuerzo, sus unidades de lucha son de 58" {
		armadura.refuerzo(new Bendicion())
		assert.equals(armadura.unidadesDeLucha(rolando.nivelDeHechiceria(), rolando.artefactos()), 58)
	}

	test "Si la armadura tiene la cota de malla de refuerzo, sus unidades de lucha son de 3" {
		armadura.refuerzo(new CotaDeMalla())
		assert.equals(armadura.unidadesDeLucha(rolando.nivelDeHechiceria(), rolando.artefactos()), 3)
	}

	test "Si la armadura tiene el hechizo básico de refuerzo, sus unidades de lucha son de 12" {
		armadura.refuerzo(new HechizoBasico())
		assert.equals(armadura.unidadesDeLucha(rolando.nivelDeHechiceria(), rolando.artefactos()), 12)
	}

	test "Si la armadura tiene el espectro maléfico de refuerzo, sus unidades de lucha son de 19" {
		armadura.refuerzo(new EspectroMalefico())
		assert.equals(armadura.unidadesDeLucha(rolando.nivelDeHechiceria(), rolando.artefactos()), 19)
	}

	test "Si rolando tiene solo un espejo en su inventario, tiene un valor de lucha de 1" {
		rolando.artefactos().clear()
		rolando.agregaArtefacto(espejo)
		assert.equals(rolando.habilidadDeLucha(), 1)
	}

	test "Si rolando tiene un libro de hechizos con un espectro maléfico y un hechizo básico, su nivel de hechicería es de 56" {
		const libroDeHechizos = new LibroDeHechizos(hechizos = [new EspectroMalefico(), new HechizoBasico()])
		rolando.hechizoPreferido(libroDeHechizos)
		assert.equals(rolando.nivelDeHechiceria(), 56)
	}

}