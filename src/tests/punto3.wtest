import rolando.*

describe "Tests del punto 3: Lucha y hechicería avanzada" {

	const rolando
	const espadaDelDestino
	const collarDivino
	const mascaraOscura
	const armadura

	fixture {
		espadaDelDestino = new Espada()
		collarDivino = new CollarDivino(perlas = 5)
		mascaraOscura = new MascaraOscura(indiceDeOscuridad = 1, minimoDePoder = 4)
		armadura = new Armadura(valorBaseDeRefuerzo = 2)
		rolando = new Personaje(hechizoPreferido = new Logos(constanteProporcionalDeHechizo = 1, nombre = "Espectro malefico"))
		rolando.artefactos([espadaDelDestino, collarDivino, mascaraOscura, armadura, espejo])
	}

	test "Si rolando esta cargado, tiene 5 artefactos o más" {
		assert.that(rolando.estasCargado())
	}

	test "Si le sacamos la armadura, no está cargado" {
		rolando.removeArtefacto(armadura)
		assert.notThat(rolando.estasCargado())		
	}

	test "Si rolando tiene los artefactos de arriba, su habilidad de lucha es de 20" {
		assert.equals(rolando.habilidadDeLucha(), 20)
	}

	test "Si la armadura tiene la bendición como refuerzo, sus unidades de lucha son de 58" {
		armadura.refuerzo(bendicion)
		assert.equals(armadura.unidadesDeLucha(rolando), 58)
	}

	test "Si la armadura tiene la cota de malla de refuerzo, sus unidades de lucha son de 3" {
		armadura.refuerzo(new CotaDeMalla())
		assert.equals(armadura.unidadesDeLucha(rolando), 3)
	}

	test "Si la armadura tiene el hechizo básico de refuerzo, sus unidades de lucha son de 12" {
		armadura.refuerzo(hechizoBasico)
		assert.equals(armadura.unidadesDeLucha(rolando), 12)
	}

	test "Si la armadura tiene el espectro maléfico de refuerzo, sus unidades de lucha son de 19" {
		armadura.refuerzo(new Logos(constanteProporcionalDeHechizo = 1, nombre = "Espectro malefico"))
		assert.equals(armadura.unidadesDeLucha(rolando), 19)
	}

	test "Si rolando tiene solo un espejo en su inventario, tiene un valor de lucha de 1" {
		rolando.artefactos().clear()
		rolando.agregaArtefacto(espejo)
		assert.equals(rolando.habilidadDeLucha(), 1)
	}

	test "Si rolando tiene un libro de hechizos con un espectro maléfico y un hechizo básico, su nivel de hechicería es de 56" {
		const libroDeHechizos = new LibroDeHechizos(hechizos = [new Logos(constanteProporcionalDeHechizo = 1, nombre = "Espectro malefico"), hechizoBasico])
		rolando.hechizoPreferido(libroDeHechizos)
		assert.equals(rolando.nivelDeHechiceria(), 56)
	}
	
	test "Si a rolando le agregamos una armadura con valor base de 5 y refuerzo de cota de malla de valor 6, su habilidad para la lucha sera de 37" {
		rolando.agregaArtefacto(new Armadura(valorBaseDeRefuerzo = 5, refuerzo = new CotaDeMalla(unidades = 6)))
		assert.equals(rolando.habilidadDeLucha(), 37)
	}

	test "Si rolando tiene una armadura extra con valor base de refuerzo 5 y un refuerzo de cota de malla con 6 de valor de lucha, su habilidad de lucha es de 37" {
		const armaduraExtra = new Armadura(valorBaseDeRefuerzo = 5, refuerzo = new CotaDeMalla(valorDeRefuerzo = 6))
		rolando.agregaArtefacto(armaduraExtra)
		assert.equals(rolando.habilidadDeLucha(), 37)
	}

}