import rolando.*
import hechiceria.*
import artefactos.*


describe "Tests del punto 4: Comercio" {

	const xenia
	const thor
	const loki
	const alacachulaCachicomula
	const mockHnos

	fixture{
		alacachulaCachicomula = new Logos(constanteProporcionalDeHechizo = 1, nombre = "alacachula cachicomula")
		xenia = new Personaje(hechizoPreferido = alacachulaCachicomula)
		thor = new Personaje(hechizoPreferido = hechizoBasico)
		loki = new Personaje(hechizoPreferido = hechizoBasico, oro = 5)
		mockHnos = new Comerciante(tipoDeComercio = new Independiente(impuesto = 0))
	}
	
	test "el precio de lista de alacachula cachicomula es 22" {
		assert.equals(alacachulaCachicomula.precio(), 22)
	}
	
	test "xenia canjea el hechizo por un hechizo basico y le quedan 100 monedas" {
		xenia.canjea(hechizoBasico)
		assert.equals(xenia.oro(), 100)
	}
	
	test "thor canjea el hechizo alacachula cachicomula y le queda 83 de oro" {
		thor.canjea(alacachulaCachicomula)
		assert.equals(thor.oro(), 83)
	}
	
	test "thor canjea 2 veces el hechizo basico y le quedan 90 de oro" {
		thor.canjea(hechizoBasico)
		thor.canjea(hechizoBasico)
		assert.equals(thor.oro(), 90)
	}
	
	test "loki quiere canjear el hechizo alacachula cachicomula pero no le alcanza el oro" {
		const excepcionEsperada = new ExcepcionPorPocoOro("No te alcanza el oro para comprar este hechizo")
		const bloqueAExplotar = {loki.canjea(alacachulaCachicomula)}
		assert.throwsExceptionLike(excepcionEsperada, bloqueAExplotar)
	}
	
	test "xenia compra una espada, un collar divino de 5 perlas, una armadura sin refuerzo, y otra armadura con cota de malla con valor de lucha 5; le quedan 85,5 de oro, y tiene 4 artefactos" {
		xenia.compra(new Espada(), mockHnos)
		xenia.compra(new CollarDivino(perlas = 5), mockHnos)
		xenia.compra(new Armadura(), mockHnos)
		xenia.compra(new Armadura(refuerzo = new CotaDeMalla(unidades = 5)), mockHnos)
		assert.equals(xenia.oro(), 85.5)
		assert.equals(xenia.artefactos().size(), 4)
	}
	
	test "thor compra una armadura con bendicion y otra armadura creforzada con hechizo alacachula; le queda 74 de oro" {
		thor.compra(new Armadura(refuerzo = bendicion), mockHnos)
		thor.compra(new Armadura(refuerzo = alacachulaCachicomula), mockHnos)
		assert.equals(thor.oro(), 74)
	}
	
	test "loki quiere comprar el espejo pero no le alcanza el oro" {
		const excepcionEsperada = new ExcepcionPorPocoOro("No te alcanza el oro para comprar este artefacto")
		const bloqueAExplotar = {loki.compra(espejo, mockHnos)}
		assert.throwsExceptionLike(excepcionEsperada, bloqueAExplotar)
	}
	
	test "xenia canjea un libro de hechizos que contiene alacachula y el hechizo basico; le quedan 69 monedas" {
		xenia.canjea(new LibroDeHechizos(hechizos = [alacachulaCachicomula, hechizoBasico]))
		assert.equals(xenia.oro(), 69)
	}

}